# ==============================================================================
#
#	CMake template file for uninstalling all installed files
#
# ==============================================================================

#
#   This policy determines whether the list command will ignore
#   empty elements in the list. CMake 2.4 and below list commands
#   ignored all empty elements in the list. The OLD behavior for
#   this policy is to ignore empty list elements. The NEW behavior
#   for this policy is to correctly count empty elements in a list.
#

if(COMMAND cmake_policy)
    if(POLICY CMP0007)
	CMAKE_POLICY(SET CMP0007 OLD)
    ENDIF()
ENDIF()

# Make sure install_manifest.txt exists
if (NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
    message(FATAL_ERROR "Cannot find install manifest: \"@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt\"")
endif()

# Open install_manifest.txt for reading
file(READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" files)

# Make some ajustments to the filenames
string(REGEX REPLACE "\n" ";" files "${files}")
list(REVERSE files)

# Iterate through all the installed files and remove them
foreach (file ${files})
    message(STATUS "Uninstalling: $ENV{DESTDIR}${file}")
    if (EXISTS "$ENV{DESTDIR}${file}")
	execute_process(
	    COMMAND @CMAKE_COMMAND@ -E remove "$ENV{DESTDIR}${file}"
	    OUTPUT_VARIABLE rm_out
	    RESULT_VARIABLE rm_retval
	)
	if(NOT ${rm_retval} EQUAL 0)
            message(FATAL_ERROR "Problem while removing \"$ENV{DESTDIR}${file}\"")
	endif ()
    else ()
	message(STATUS "File \"$ENV{DESTDIR}${file}\" does not exist.")
    endif ()
endforeach(file)
